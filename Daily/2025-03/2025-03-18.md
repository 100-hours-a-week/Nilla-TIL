## 날짜: 2025-03-18

### 스크럼
- 테라폼 익혀보기

### 새로 배운 내용
#### 테라폼
```bash
provider "aws" {
 region = "us-east-1"
 default_tags{
  tags={
   Name= "student0"
   Subject = "cloud-programming"
   Chapter = "practice3"
}
}
}

variable "vpc_main_cidr"{
 description= "VPC main CIDR block"
 default = "10.0.0.0/23"
}

resource "aws_vpc" "my_vpc" {
 cidr_block = var.vpc_main_cidr
 instance_tenancy = "default"
 enable_dns_hostnames= true
}

resource "aws_vpc_ipv4_cidr_block_association" "secondary_cidr"{
	vpc_id = aws_vpc.my_vpc.id
	cidr_block = "10.1.0.0/23"
}

resource "aws_subnet" "pub_sub_1" {
  vpc_id            = aws_vpc.my_vpc.id
  cidr_block        = cidrsubnet(aws_vpc.my_vpc.cidr_block, 1, 0)
  availability_zone = "us-east-1a"
  map_public_ip_on_launch = true
}

resource "aws_subnet" "prv_sub_1" {
  vpc_id            = aws_vpc.my_vpc.id
  cidr_block        = cidrsubnet(aws_vpc.my_vpc.cidr_block, 1, 1)
  availability_zone = "us-east-1a"
}

resource "aws_subnet" "pub_sub_2" {
  vpc_id            = aws_vpc.my_vpc.id
  cidr_block        = cidrsubnet(aws_vpc_ipv4_cidr_block_association.secondary_cidr.cidr_block, 1, 0)
  availability_zone = "us-east-1b"
  map_public_ip_on_launch = true
}

resource "aws_subnet" "prv_sub_2" {
  vpc_id            = aws_vpc.my_vpc.id
  cidr_block        = cidrsubnet(aws_vpc_ipv4_cidr_block_association.secondary_cidr.cidr_block, 1, 1)
  availability_zone = "us-east-1b"
}

resource "aws_internet_gateway" "my_igw" {
	vpc_id = aws_vpc.my_vpc.id
}

resource "aws_route_table" "pub_rt"{
	vpc_id = aws_vpc.my_vpc.id
	route{
		cidr_block = "0.0.0.0/0"
		gateway_id = aws_internet_gateway.my_igw.id
	}
}

resource "aws_route_table" "prv_rt1"{
	vpc_id = aws_vpc.my_vpc.id
	route{
	cidr_block = "0.0.0.0/0"
	nat_gateway_id = aws_nat_gateway.nat_gw_1.id
	}
}
resource "aws_route_table" "prv_rt2"{
	vpc_id = aws_vpc.my_vpc.id
	route{
	cidr_block = "0.0.0.0/0"
	nat_gateway_id = aws_nat_gateway.nat_gw_2.id
	}
}

resource "aws_route_table_association" "pub_rt_asso"{
        subnet_id= aws_subnet.pub_sub_1.id
        route_table_id = aws_route_table.pub_rt.id
}
resource "aws_route_table_association" "pub_rt_asso1"{
        subnet_id= aws_subnet.pub_sub_2.id
        route_table_id = aws_route_table.pub_rt.id
}
resource "aws_route_table_association" "prv_rt1_asso"{
        subnet_id= aws_subnet.prv_sub_1.id
        route_table_id = aws_route_table.prv_rt1.id
}
resource "aws_route_table_association" "prv_rt2_asso"{
        subnet_id= aws_subnet.prv_sub_2.id
        route_table_id = aws_route_table.prv_rt2.id
}

resource "aws_eip" "nat_eip1" {
domain = "vpc"
}
resource "aws_eip" "nat_eip2" {
domain = "vpc"
}

resource "aws_nat_gateway" "nat_gw_1"{
	allocation_id = aws_eip.nat_eip1.id
	subnet_id = aws_subnet.pub_sub_1.id
	depends_on = [aws_internet_gateway.my_igw]
}
resource "aws_nat_gateway" "nat_gw_2"{
	allocation_id = aws_eip.nat_eip2.id
	subnet_id = aws_subnet.pub_sub_2.id
	depends_on = [aws_internet_gateway.my_igw]
}
```

```bash
# cluster.tf
resource "aws_security_group" "webserver_sg" {
  name   = "webserver-sg-student0"
  vpc_id = aws_vpc.my_vpc.id

  ingress {
    from_port   = var.server_port
    to_port     = var.server_port
    protocol    = "tcp"
    cidr_blocks = [
      aws_subnet.pub_sub_1.cidr_block,
      aws_subnet.pub_sub_2.cidr_block
    ]
  }
}

#자동확장그룹 작성
resource "aws_launch_template" "webserver_template" {
  image_id      = "ami-0ea4029a71f24b319"
  instance_type = "t3.micro"

  user_data = base64encode(<<-EOF
    #!/bin/bash
    # 시스템 패키지 업데이트 및 busybox 설치
    yum update -y
    yum install -y busybox

    # 웹 서버 루트 디렉토리 생성
    mkdir -p /var/www

    # 기본 웹 페이지 생성
    echo "Hello, World" > /var/www/index.html

    # busybox 웹 서버 실행 (백그라운드 실행)
    nohup busybox httpd -f -p 80 -h /var/www &

    # 실행 확인 로그
    echo "BusyBox HTTP server started on port 80"
  EOF
  )
}

resource "aws_autoscaling_group" "webserver_asg"{
  vpc_zone_identifier = [aws_subnet.pub_sub_1.id, aws_subnet.pub_sub_2.id]
  #자동확장 그룹에 대상 그룹지정
  target_group_arns   = [aws_lb_target_group.target_asg.arn]
  health_check_type   = "ELB"
	min_size = 2
	max_size = 3
	launch_template {
		id = aws_launch_template.webserver_template.id
		version = "$Latest"
	}
	depends_on = [aws_vpc.my_vpc, aws_subnet.pub_sub_1, aws_subnet.pub_sub_2]
}

#alb작성
resource "aws_security_group" "alb_sg" {
  name   = var.alb_security_group_name
  vpc_id = aws_vpc.my_vpc.id

  ingress {
    from_port   = var.server_port
    to_port     = var.server_port
    protocol    = "tcp"
    cidr_blocks = [var.my_ip]
  }
  
   egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
}

#alb 리소스 생성
resource "aws_lb" "webserver_alb"{
	name = var.alb_name
	load_balancer_type = "application"
	subnets = [aws_subnet.pub_sub_1.id, aws_subnet.pub_sub_2.id]
	security_groups =[aws_security_group.alb_sg.id] 
}

#alb 대상 그룹 생성
resource "aws_lb_target_group" "target_asg" {
  name     = var.alb_name
  port     = var.server_port
  protocol = "HTTP"
  vpc_id   = aws_vpc.my_vpc.id

  health_check {
    path                 = "/"
    protocol             = "HTTP"
    matcher              = "200"
    interval             = 15
    timeout              = 2
    healthy_threshold    = 2
    unhealthy_threshold  = 2
  }
}

#리스너 생성
resource "aws_lb_listener" "http" {
  load_balancer_arn = aws_lb.webserver_alb.arn
  port              = var.server_port
  protocol          = "HTTP"

  default_action {
    type           = "forward"
    target_group_arn = aws_lb_target_group.target_asg.arn
  }
}
#리스너 규칙 지정
resource "aws_lb_listener_rule" "webserver_asg_rule" {
  listener_arn = aws_lb_listener.http.arn
  priority     = 100

  condition {
    path_pattern {
      values = ["*"]
    }
  }

  action {
    type           = "forward"
    target_group_arn = aws_lb_target_group.target_asg.arn
  }
}

```

```bash
# variables.tf
variable "server_port" {
  description = "Webserver’s HTTP port"
  type        = number
  default     = 80
}

variable "my_ip" {
  description = "My public IP"
  type        = string
  default     = "0.0.0.0/0"
}

#alb 작성
variable "alb_security_group_name"{
	description = "the name of the ALB's security group"
	type = string
	default = "webserver-alb-sg-student0"
}

variable "alb_name"{
	description = "The name of ALB"
	type = string
	default = "webserver-alb-student0"
}
```

```bash
# output.tf
output "alb_dns_name" {
	value = aws_lb.webserver_alb.dns_name
	description = "the domain name of the load balancer"
}
```

### 오늘의 회고
- 우선 정리 없이 쭉 구성을 해보았다. 중간중간 트러블 슈팅도 많았는데, 관련 내용과 상세 코드 정리를 내일 개념 정리를 더해서 진행하려고 한다. 

### 참고 자료 및 링크
- x